# Maintainer: Chocobo1 <chocobo1 AT archlinux DOT net>

pkgname=xrdp
pkgver=0.9.14
pkgrel=1
pkgdesc="An open source remote desktop protocol (rdp) server"
arch=('i686' 'x86_64')
url="http://www.xrdp.org/"
license=('Apache')
depends=('glibc' 'fuse' 'libjpeg-turbo' 'libxfixes' 'libxrandr' 'lame' 'opus' 'pam' 'pixman')
makedepends=('nasm')
optdepends=('tigervnc: optional backend')
backup=('etc/xrdp/cert.pem' 'etc/xrdp/key.pem' 'etc/xrdp/rsakeys.ini' 'etc/xrdp/sesman.ini' 'etc/xrdp/xrdp.ini')
options=('staticlibs')
source=("https://github.com/neutrinolabs/xrdp/releases/download/v$pkgver/xrdp-$pkgver.tar.gz"{,.asc}
        "arch-config.patch::https://aur.archlinux.org/cgit/aur.git/plain/arch-config.diff?h=xrdp")
sha256sums=('e3a9d27da7881dbfb7fd22b33c11dd7390d42ca8ff94541e88f552b8dce1b5d2'
            'SKIP'
            'SKIP')
validpgpkeys=('61ECEABBF2BB40E3A35DF30A9F72CDBC01BF10EB')  # Koichiro IWAO <meta@vmeta.jp>


prepare() {
  cd "$pkgname-$pkgver"

  patch -Np2 -i "$srcdir/arch-config.patch"
}

build() {
  cd "$pkgname-$pkgver"

  ./configure \
    --prefix="/usr" \
    --sysconfdir="/etc" \
    --localstatedir="/var" \
    --sbindir="/usr/bin" \
    --with-systemdsystemunitdir="/usr/lib/systemd/system" \
    --enable-fuse \
    --enable-ipv6 \
    --enable-mp3lame \
    --enable-opus \
    --enable-pixman \
    --enable-tjpeg \
    --enable-vsock
  make
}

check() {
  cd "$pkgname-$pkgver"

  #make check
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}
