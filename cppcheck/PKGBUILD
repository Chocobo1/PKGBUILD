# Maintainer: Chocobo1 <chocobo1 AT archlinux DOT net>

pkgname=cppcheck
pkgver=2.2
pkgrel=1
pkgdesc="Static analysis tool for C/C++ code"
arch=('i686' 'x86_64')
url="http://cppcheck.sourceforge.net/"
license=('GPL3')
depends=('hicolor-icon-theme' 'qt5-base')
makedepends=('cmake' 'docbook-xsl' 'python' 'qt5-tools')
source=("$pkgname-$pkgver-src.tar.gz::https://github.com/danmar/cppcheck/archive/$pkgver.tar.gz")
sha256sums=('ad96290a1842c5934bf9c4268cb270953f3078d4ce33a9a53922d6cb6daf61aa')


prepare() {
  cd "$pkgname-$pkgver"

  mkdir -p "_build"
}

build() {
  cd "$pkgname-$pkgver/_build"

  cmake \
    -DCMAKE_INSTALL_PREFIX="/usr" \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_GUI=yes \
    -DHAVE_RULES=yes \
    ../
  make
}

package() {
  cd "$pkgname-$pkgver/_build"

  make DESTDIR="$pkgdir" install
}
