# Maintainer: Chocobo1 <chocobo1 AT archlinux DOT net>

pkgname=cppcheck
pkgver=1.90
pkgrel=1
pkgdesc="Static analysis tool for C/C++ code"
arch=('i686' 'x86_64')
url="http://cppcheck.sourceforge.net/"
license=('GPL3')
depends=('hicolor-icon-theme' 'qt5-base' 'qt5-charts')
makedepends=('cmake' 'docbook-xsl' 'python' 'qt5-tools')
source=("$pkgname-$pkgver-src.tar.gz::https://github.com/danmar/cppcheck/archive/$pkgver.tar.gz")
sha256sums=('c4864d3e09359214efdd503b52e241f4f56ba7ce26f8c11939fd9dcfac1fd105')


prepare() {
  cd "$pkgname-$pkgver"

  mkdir -p "_build"
}

build() {
  cd "$pkgname-$pkgver/_build"

  cmake \
    -DCMAKE_INSTALL_PREFIX="/usr" \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_GUI=yes \
    -DHAVE_RULES=yes \
    -DHAVE_QCHART=yes \
    ../
  make
}

package() {
  cd "$pkgname-$pkgver/_build"

  make DESTDIR="$pkgdir" install
}
